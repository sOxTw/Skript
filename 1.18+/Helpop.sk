#!------------------------------------------------
#!     __  __     __                 _____ __ __
#!    / / / /__  / /___  ____  ____ / ___// //_/
#!   / /_/ / _ \/ / __ \/ __ \/ __ \\__ \/ ,<   
#!  / __  /  __/ / /_/ / /_/ / /_/ /__/ / /| |  
#! /_/ /_/\___/_/ .___/\____/ .___/____/_/ |_|  
#!             /_/         /_/                  
#!------------------------------------------------

#! This plugin is based on [EssentialsX] a Bukkit/Spigot plugin that is responsible for providing mixed tools to the server. In which is the command [/helpop].
#! This is where HelpopSk intervenes and is responsible for managing the messages that the user sends to the staff and the staff to the user.

#! .-.-.-.-.-.-.-.-.-.-.-. Using .-.-.-.-.-.-.-.-.-.-.-.
#! # [Skript 2.6.2] #! For proper functioning of the plugin for Essentials Plugin.
#! # [SkQuery 4.1.6] #! It is required for the use of parts of the code, part of the syntax.
#! .-.-.-.-.-.-.-.-.-.-.-. ----- .-.-.-.-.-.-.-.-.-.-.-.

#! .-.-.-.-.-.-.-.-.-.-.-. Permissions .-.-.-.-.-.-.-.-.-.-.-.
#! essentials.helpop.receive # It is responsible for notifying those who have said permission when a STAFF or USER uses the /helpop command.
#! helpopsk.chat.op # It is responsible for notifying those who have said permission when a ADMIN/OP uses the /op <message> command.
#! helpopsk.chat.mod # It is responsible for notifying those who have said permission when a STAFF uses the /mod <message> command.
#! .-.-.-.-.-.-.-.-.-.-.-. ----------- .-.-.-.-.-.-.-.-.-.-.-.

options:
	# Permisos:
	Permiso-helpop-receive: "essentials.helpop.receive" # Permiso de notificaciones [Ayuda de Helpop].
	Permiso-chatmod: "chat.mod" # Permiso para chat de MODERADOR.
	Permiso-chatadmin: "chat.op" # Permiso para chat OP.
	
	Prefix-msarg1: %{_player}% &e» &c&l # Prefijo de mensaje sin respuesta.
	Prefix-msstaff: &c&lTu > %{_playerplayer}% &f» &7 # Prefijo de mensaje sin respuesta.
	
	# Mensajes:
	Razon: "Mal uso de helpop!" # Razón de un mute sin especificar motivo.
	Razon-Inside: Mal uso de helpop! # Razón de un mute sin especificar motivo sin "".
	
	# Tiempo:
	Cooldown: 8 seconds #Tiempo sin mute!

on load:
	set {_PathConfig} to "plugins/HelpopSk/Config.yml"
	if file {_PathConfig} does not exist:
		set yaml value "LangHelpopSk" from {_PathConfig} to "En"
		# English
		set yaml value "LangEnglish.Prefix" from {_PathConfig} to "&4&l[HelpOP]"
		set yaml value "LangEnglish.OPChat" from {_PathConfig} to "&7[&9OPC&7]"
		set yaml value "LangEnglish.ModChat" from {_PathConfig} to "&7[&5MD&7]"
		set yaml value "LangEnglish.HSpy" from {_PathConfig} to "&7[&b&lHSpy&7]"
		set yaml value "LangEnglish.Warn" from {_PathConfig} to "&7[&c&l!&7]"
		# Spanish
		set yaml value "LangSpanish.Prefix" from {_PathConfig} to "&4&l[Ayuda de OP]"
		set yaml value "LangSpanish.OPChat" from {_PathConfig} to "&7[&9OPC&7]"
		set yaml value "LangSpanish.ModChat" from {_PathConfig} to "&7[&5MD&7]"
		set yaml value "LangSpanish.HSpy" from {_PathConfig} to "&7[&b&lHSpy&7]"
		set yaml value "LangSpanish.Warn" from {_PathConfig} to "&7[&c&l!&7]"

		wait 2 seconds
		execute console command "sk reload HelpopSk" #!Important

	else:
		set {_getPathLang} to yaml value "LangHelpopSk" from {_PathConfig}
		set {_getEncglishPrefix} to yaml value "LangEnglish.Prefix" from {_PathConfig}
		set {_getSpanishPrefix} to yaml value "LangSpanish.Prefix" from {_PathConfig}
		if {_getPathLang} is "En":
			# Get value's
			set {_getPrefix} to yaml value "LangEnglish.Prefix" from {_PathConfig}
			set {_getOpChat} to yaml value "LangEnglish.OPChat" from {_PathConfig}
			set {_getModChat} to yaml value "LangEnglish.ModChat" from {_PathConfig}
			set {_getHspy} to yaml value "LangEnglish.HSpy" from {_PathConfig}
			set {_getWarn} to yaml value "LangEnglish.Warn" from {_PathConfig}
			# Set Value's from Global
			set {Prefix} to {_getPrefix}
			set {OpChat} to {_getOpChat}
			set {ModChat} to {_getModChat}
			set {Warn} to {_getWarn}
			set {Lang} to {_getPathLang}
			# Send properties load files
			send "%{_getEncglishPrefix}% &7> &bI load the configuration in &eEnglish." to console
		if {_getPathLang} is "Es":
			# Get value's
			set {_getPrefix} to yaml value "LangSpanish.Prefix" from {_PathConfig}
			set {_getOpChat} to yaml value "LangSpanish.OPChat" from {_PathConfig}
			set {_getModChat} to yaml value "LangSpanish.ModChat" from {_PathConfig}
			set {_getHspy} to yaml value "LangSpanish.HSpy" from {_PathConfig}
			set {_getWarn} to yaml value "LangSpanish.Warn" from {_PathConfig}
			# Set Value's from Global
			set {Prefix} to {_getPrefix}
			set {OpChat} to {_getOpChat}
			set {ModChat} to {_getModChat}
			set {HSpy} to {_getHspy}
			set {Warn} to {_getWarn}
			set {Lang} to {_getPathLang}
			# Send properties load files
			send "%{_getSpanishPrefix}% &7> &bCargo la configuracion en &eEspañol." to console


# Function setTime
function setTime(t: text) :: text:
	set {_tm} to {_t}
	set {_numbers::*} to {_tm} split at "d"
	if {_tm} contains "d":
		set {_day} to {_numbers::1} parsed as numbers
		replace "%{_day}%d" with "" in {_tm}

	set {_numbers::*} to {_tm} split at "h"
	if {_tm} contains "h":
		set {_hour} to {_numbers::1} parsed as numbers
		replace "%{_hour}%h" with "" in {_tm}

	set {_numbers::*} to {_tm} split at "m"
	if {_tm} contains "m":
		set {_min} to {_numbers::1} parsed as numbers
		replace "%{_min}%m" with "" in {_tm}

	set {_numbers::*} to {_tm} split at "s"
	if {_tm} contains "s":
		set {_seg} to {_numbers::1} parsed as numbers
		replace "%{_seg}%s" with "" in {_tm}

	if {_day} is set:
		add "%{_day}% days" to {_final::*}

	if {_hours} is set:
		add "%{_hours}% hours" to {_final::*}

	if {_min} is set:
		add "%{_min}% minutes" to {_final::*}

	if {_seg} is set:
		add "%{_seg}% seconds" to {_final::*}

	set {_result} to "%{_final::*}%"
	replace all "," with "" in {_result}

	return {_result}

# Function ModifyTime
function modifyTime(data: text) :: text:
	replace all " " with "" in {_data}
	replace "minutes" and "minute" with "min" in {_data}
	replace "seconds" and "second" with "seg" in {_data}
	replace "hours" and "hour" with "hs" in {_data}
	replace "and" with ":" in {_data}
	return {_data}
# Function getTag
Function getTag(p: player) :: text:
	set {_getTag} to "%{_p}'s prefix%%{_p}%"
	replace "&" with "§" in {_getTag}
	replace all "§k" with "" in {_getTag}
	replace all "|" and "!" and "¡" with "" in {_getTag}
	return {_getTag}
# Function Capitalation
function capitalation(text: text, char: int) :: text:
	set {_Text} to last (length of {_Text} - {_char}) characters of {_Text}
	set {_TextUpper} to the first character of {_Text} in upper case
	set {_Text} to last (length of {_Text} - 1) characters of {_Text}
	return "%{_TextUpper}%%{_Text}%"

# Modify command "/helpop":
command /helpop [<text>]:
	trigger:
		if arg 1 is not set:
			send "%{Warn}% &cNecesitas colocar tu duda o reporte."
			stop
		else if arg 1 contains "&":
			send "%{Warn}% &c&lNo puedes usar colores en la ayuda para el staff."
			stop
		else:
			if {HMute::%player%} is set:
				if {HMute::%player%} > 1 second:
					set {_time} to "%{HMute::%player%}%"
					add "%player%" to yaml list "Users.Muted.%player's uuid%" from "plugins/HelpopSk/Muted.yml"
					if {Lang} is "En":
						send ""
						send "%{Warn}% &cYou are muted from Helpop for: &d%{_time}%"
						send "%{Warn}% &cReason: &6&l%{Mutearg3::%player%}%"
						send ""
						stop
					if {Lang} is "Es":
						send ""
						send "%{Warn}% &cEstas silenciado de Helpop por: &d%{_time}%"
						send "%{Warn}% &cMotivo: &6&l%{Mutearg3::%player%}%"
						send ""
						stop
			else:
				set {_waited} to difference between {mute.%player%.lastused} and now
				if {_waited} is less than {@Cooldown}:
					set {_time} to "%difference between {@Cooldown} and {_waited}%"
					# replace " seconds" and " second" with "s" in {_time}
					# replace " minutes" and " minute" with "m" in {_time}
					# replace " and " with ":" in {_time}
					send ""
					send "%{Warn}% &cEspera antes de usar Helpop de nuevo: &d%{_time}%"
					send ""
					stop
				send ""
				send "%{Warn}%  &aEl staff ya fue notificado con tu consulta o reporte."
				send ""
				loop all players:
					if loop-player has permission "essentials.helpopp.recive":
						delete {HMute::%player%} and {Mutearg3::%player%}
						set {_tag} to getTag(player)
						set {_message} to capitalation(arg 1, 0)
						set {mute.%player%.lastused} to now
						play sound "entity.player.levelup" with pitch 6 to loop-player
						if arg 1 contains "http://" or "https://" or "www" or ".com" or ".net" or ".org" or ".info" or ".es":
							send "%{Prefix}% &a&l%{_player}% &7» &f&l%arg-1%" to loop-player
						else:
							send formatted "<ttp:&a[Clic] &fPara silenciar a &b%player%><cmd:/__opens %player%>%{Prefix}%<reset><ttp:&a[Clic] &fPara ir con &b%player%><cmd:/tp %player%>&7[&6&lTP&7]<reset><ttp:&a[Clic] &fPara ir enviar mensaje a: &b%player%><sgt:/h %player% >&7[&d&lM&7]<reset><ttp:&a[Clic] &fpara responder a: &b%player%><sgt:%player% > %{_tag}% &7» &f&l%{_message}%<reset>" to loop-player
							
# Interfaz, al usar los mensajes interactivos "[Ayuda de OP]"							
command /__opens [<offline player>]:
	trigger:
		if player has permission "essentials.helpopp.recive":
			set {_tag} to getTag(player)
			open chest with 1 rows named "&8&l- - - SANCIONES &8&l- - -" to player
			wait 5 ticks
			format slot 0 of player with lime stained glass pane named "&aSilenciar por 5 Minutos" with lore "%nl%&7Sancionar por 5 Min.%nl%&7a %arg 1%&7.%nl%%nl%&7- &eMotivo: &6&l{@Razon-Inside}" to close then run [execute player command "hmute %arg-1% 5m"]
			
			format slot 1 of player with yellow stained glass pane named "&eSilenciar por 20 Minutos" with lore "%nl%&7Sancionar por 20 Min.%nl%&7a %arg 1%&7.%nl%%nl%&7- &eMotivo: &6&l{@Razon-Inside}" to close then run [execute player command "hmute %arg-1% 20m"]
			
			format slot 2 of player with orange stained glass pane named "&6Silenciar por 30 Minutos" with lore "%nl%&7Sancionar por 30 Min.%nl%&7a %arg 1%&7.%nl%%nl%&7- &eMotivo: &6&l{@Razon-Inside}" to close then run [execute player command "hmute %arg-1% 30m"]
			
			format slot 3 of player with red stained glass pane named "&4Silenciar por 1 Hora" with lore "%nl%&7Sancionar por 1 Hora.%nl%&7a %arg 1%&7.%nl%%nl%&7- &eMotivo: &6&l{@Razon-Inside}" to close then run [execute player command "hmute %arg-1% 1h"]
			
			format slot 6 of player with gray dye named "&cDesilenciar" with lore "%nl%&7Remover silencio.%nl%&7a %arg 1%&7." to close then run [execute player command "hunmute %arg-1%"]
			
			format slot 8 of player with book named "&9Información" with lore "%nl%&7Ver información de%nl%&7sanciones para:%nl%%arg 1%&7." to close then run [execute player command "htime %arg-1%"]
							
# Establece el tiempo de mute | #! NO MODIFICAR!						
command /hmute [<offline player>] [<text>] [<text>]:
	trigger:
		if player has permission "essentials.helpopp.recive":
			if arg 1 is set:
				if arg 2 is set:
					set {mute.%arg-1%.lastused} to now
					delete {Mutearg3::%arg-1%}
					set {_arg2} to arg 2
					set {Mutearg3::%arg-1%} to arg 3
					if {Mutearg3::%arg-1%} is not set:
						set {Mutearg3::%arg-1%} to {@Razon}
					if {_arg2} is set:
						set {_clearData} to setTime({_arg2})
						set {_setData} to modifyTime({_clearData})
						set {HMute::%arg-1%} to {_clearData} parsed as timespan
						send "%{Warn}% &a%player% &7Te ha sido silenciado de %{Prefix}% &7Motivo: &c&l%{Mutearg3::%arg-1%}% &e&l%{_setData}%." to arg 1
						
					else:
						send "%{Warn}% &e/hmute &7<&eusuario&7> &7<&etiempo&7> &7[&erazón&7]."
						stop
				else:
					send "%{Warn}% &e/hmute &7<&eusuario&7> &7<&etiempo&7> &7[&erazón&7]."
					send "&c&lTiempos: &fs=Segundos, m=Minutos, h=Horas, d=Dias"
					stop
			else:
				send "%{Warn}% &e/hmute &7<&eusuario&7> &7<&etiempo&7> &7[&erazón&7]."
				stop
		else:
			send "%{Warn}% &cNo tienes permisos para usar este comando."
			stop
			
# Remueve el mute dado | #!NO MODIFICAR!		
command /hunmute [<offline player>]:
	trigger:
		if player has permission "essentials.helpopp.recive":
			if arg 1 is set:
				if {HMute::%arg-1%} is set:
					delete {HMute::%arg-1%}
					set {mute.%arg-1%.lastused} to now
					send "%{Warn}% &c&l%arg-1% &ehas sido Desilenciado de Helpop!." to arg 1
					loop all players:
						if loop-player has permission "essentials.helpopp.recive":
							send "%{Warn}% &c&l%arg-1% &7Fué desilenciado de Helpop &7Por &b%player%&7." to loop-player
				else:
					send ""
					send "%{Warn}% &c&l%arg-1% &ano esta Silenciado de Helpop."
					send ""
					stop
			else:
				send "%{Warn}% &b/hunmute <usuario>."
				stop
		else:
			send "%{Warn}% &cNo tienes permisos para usar este comando."
			stop		
			
# Información del mute dado | #! NO MODIFICAR!
command /htiempo [<offline player>]:
	aliases: htime
	trigger:
		if player has permission "essentials.helpopp.recive":
			if arg 1 is set:
				if {HMute::%arg-1%} is set:
					set {_time} to "%{HMute::%arg-1%}%"
					replace every " and " with ":" in {_time}
					replace " year" and " years" with "y" in {_time}
					replace " week" and " weeks" with "w" in {_time}
					replace " day" and " days" with "d" in {_time}
					replace " hour" and " hours" and " hours " and "hs" with "h" in {_time}
					replace " minute" and " minutes" and " minutes " and "ms" with "m" in {_time}
					replace " second" and " seconds" with "" in {_time}
					send ""
					send "%{Warn}% &cTiempo restante de &e&l%arg-1%&c: &b%{_time}%"
					send "%{Warn}% &cRazón: &6&l%{Mutearg3::%arg-1%}%"
					send ""
					stop
				else:
					send ""
					send "%{Warn}% &c&l%arg-1% &ano esta Silenciado de Helpop."
					send ""
					stop
			else:
				send "%{Warn}% &cNo se pudo encontrar."
				send "%{Warn}% &b/htiempo &7<&eusuario&7>."
				stop
		else:
			send "%{Warn}% &cNo tienes permisos para usar este comando."
			stop
			
# Chat de Ops | #! NO MODIFICAR!
command /a [<text>]:
	trigger:
		if player has permission {@Permiso-chatadmin}:
			if arg 1 is set:
				loop all players:
					if loop-player has permission {@Permiso-chatadmin}:
						set {_tag} to getTag(player)
						send "%{OpChat}% %{_tag}% &c» &9&l%arg-1%" to loop-player
			else:
				send "%{Warn}% &aNecesitas establecer un mensaje!."
				stop
				
# Chat de Moderadores | #! NO MODIFICAR!
command /mod [<text>]:
	aliases: fm, md, cm
	trigger:
		if player has permission {@Permiso-chatmod}:
			if arg 1 is set:
				loop all players:
					if loop-player has permission {@Permiso-chatmod}:
						set {_tag} to getTag(player)
						send "%{ModChat}% %{_tag}% &c» &5&l%arg-1 in kebab case%" to loop-player
			else:
				send "%{Warn}% &aNecesitas establecer un mensaje!."
				stop
				
# Comando de Mensaje Privado de [Ayuda de OP] | #! Solo modificar lo señalado.
command /helpopmsg [<player>] [<text>]:
	aliases: hmsg, helpopm, hm, h # Si causa conflicto se puede quitar algun ALIAS.
	trigger:
		if player has permission "essentials.helpopp.recive":
			if arg 1 is "%player%":
				send "%{Warn}% &cNo puedes enviarte un mensaje tu solo."
				stop
			if arg 1 is online:
				if arg 2 is set:
					# set {_player} to "%player's prefix%&b%player%"
					# set {_playerplayer} to "%arg-1's prefix%&b%arg-1%"
					# replace "&" with "§" in {_player}
					# replace "&" with "§" in {_playerplayer}
					send "{@Prefix-msstaff}&l%arg-2%"
					send "" to arg 1
					play sound "entity.fox.sniff" to arg 1
					send "{@Prefix-msarg1}&b&l%arg-2%" to arg 1
					# json("%arg 1%", "&b&oResponde este mensaje con un &3[Clic Aqui]&b o usa &3/helpop&b.%nl%ttp:&a[Clic] &fPara responder al staff%nl%sgt:/helpop ")
					send "" to arg 1
					loop all players:
						if loop-player has permission {@Permiso-chatadmin}:
							if {hmsg::%loop-player%} is true:
								set {_Ops::*} to loop-player
								loop {_Ops::*}:
									if loop-value-2 is not sender:
										send "" to loop-value-2
										send "%{HSpy}% &d%player% &5> &9%arg-1% &7> &f%arg-2%" to loop-value-2
										send "" to loop-value-2
									
				else:
					send "%{Warn}% &cNecesitas establecer un mensaje."
					stop
			else:
				send "%{Warn}% &cJugador no encontrado!"
				send "%{Warn}% &b/h &7<&eusuario&7> &7<&emensaje&7>."
				stop
		else:
			send "&cNo tienes permisos para usar este comando."
			stop
			
command /helpopmsgspy:
	aliases: hspy, hmspy
	trigger:
		if {hmsg::%player%} is not set:
			set {hmsg::%player%} to true
			send "&a&lOkey! %{HSpy}% &aActivado"
			stop
		else:
			delete {hmsg::%player%}
			send "&c&lOkey! %{HSpy}% &cDesactivado"
			stop
			
# Bloqueo, accion extra al utilizar /ac | #! ¡CUIDADO!... Muy Delicado.
on command:
	if executor is player:
		if command is equal to "eamsg" or "amsg" or "eac" or "ehelpop" or "ac":
			if player does not have permission "essentials.helpopp.recive":
				cancel event
				set {_ac} to full command
				if {_ac} is "ac" or "helpop":
					send "%{Warn}% &cNecesitas escribir tu duda o reporte!"
					stop
				else:
					replace "ac " and "helpop " with "" in {_ac}
					execute player command "helpop %{_ac}%"
					stop
			else:
				cancel event
				set {_ac} to full command
				if {_ac} is "ac":
					send "%{Warn}% &cNo colocaste un mensaje!"
					send "%{Warn}% &bSilenciar a un jugador: &e/hmute."
					send "%{Warn}% &bEstado de un jugador: &e/htiempo."
					send "%{Warn}% &bEnviar mensaje sin respuesta: &e/helpopmsg."
					if player has permission {@Permiso-chatmod}:
						send "%{Warn}% &aCanal de chat &5[MOD]: &e/md <mensaje>"
					if player has permission {@Permiso-chatadmin}:
						send "%{Warn}% &aCanal de chat &7[&bOP&7]: &e/a <mensaje>"
						send "%{Warn}% &aMensajes de Helpop Spy: &e/hspy."
					stop
				else:
					if {_ac} contains "&":
						send "%{Warn}% &cNo puedes usar colores (&) en este comando."
						stop
					else:
						
						set {_tag} to getTag(player)
						set {_ac} to capitalation({_ac}, 3)
						
						loop all players:
							if loop-player has permission "essentials.helpopp.recive":
								
								if {Lang} is "En":
									send formatted "%{Prefix}%<ttp:&a[Clic] &fGo with &9%player%><cmd:/tp %player%>&7[&6&lTP&7]<reset> <ttp:&2● &a[Clicked] &fMention &9%player%&f by chat staff><sgt:/ac %player% >%{_tag}%<reset> &7» <ttp:&2● &a[Clicked] &fPaste what appears here in your chat><sgt:%{_acUpper}%>&f&l%{_ac}%<reset>" to loop-player

								if {Lang} is "Es":

									send formatted "%{Prefix}%<ttp:&a[Clic] &fPara ir con &9%player%><cmd:/tp %player%>&7[&6&lTP&7]<reset> <ttp:&a[Clic] &fMenciona por chat staff a &9%player%><sgt:/ac %player% >%{_tag}%<reset> &7» <ttp:&a[Clic] &fPega en tu chat lo que aparece aquí><sgt:%{_ac}%>&f&l%{_ac}%<reset>" to loop-player
								
# Evenco de los canales de Chat | #! Modificar de ser necesario!
on chat:
	if {staffchat::%player%} is set:
		cancel event
		execute player command "ac %message%"
		stop
	if {modchat::%player%} is set:
		cancel event
		execute player command "md %message%"
		stop
	if {adminchat::%player%} is set:
		cancel event
		execute player command "a %message%"
		stop
		
# Cooldown global | #!No modificar!
every 1 seconds:
	loop all players:
		if loop-player is online:
			if {HMute::%loop-player%} is set:
				if {HMute::%loop-player%} <= 1 second:
					delete {HMute::%loop-player%}
					delete {Mutearg3::%loop-player%}
				if {HMute::%loop-player%} >= 2 second:
					remove 1 second from {HMute::%loop-player%}
					
# Auto vanish | #!Solo OPs
on join:
	if player has permission "sv.auto":
		wait 5 ticks
		execute player command "v on"
		set {hmsg::%player%} to true
		stop